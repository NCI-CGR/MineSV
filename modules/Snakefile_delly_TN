#!/usr/bin/env python3

import os

# snakemake -s modules/Snakefile_delly_somatic --cluster "qsub -o /DCEG/CGF/Bioinformatics/Production/Bari/Struct_var_pipeline_dev/snake_tests/ -j y -pe by_node 2" --jobs 100 --latency-wait 300

conf = os.environ.get("conf")
configfile: conf
execDir = config['execDir']
parentDir = config['outDir']
workingDir = parentDir + 'delly_TN/'
dataDir = config['inDir']
bamList = config['inFile']
ref = config['refGenome']

# read in a file where each row has the pair name, tumor file name, normal file name
# this could change depending on functionality required, eg
# if tumor and normal bams have the same name but are in different directories
# (currently assuming same directory for T and N)
bamDict = {}
with open(bamList) as f:
    for line in f:
        (pair, tumor, normal) = line.split()
        bamDict[pair] = (dataDir + tumor, dataDir + normal)

TYPES = ['DEL', 'DUP', 'INV', 'BND', 'INS']


def get_tumor_bam(wildcards):
    (tumor, normal) = bamDict[wildcards.sample]
    return tumor


def get_tumor_index(wildcards):
    (tumor, normal) = bamDict[wildcards.sample]
    return tumor + '.bai'


def get_normal_bam(wildcards):
    (tumor, normal) = bamDict[wildcards.sample]
    return normal


def get_normal_index(wildcards):
    (tumor, normal) = bamDict[wildcards.sample]
    return normal + '.bai'

rule all:
    input:
        # expand(workingDir + 'somatic/{sample}_{type}.bcf', sample=bamDict.keys(), type=TYPES),
        parentDir + 'SV_files_for_annotation_delly.txt'
        # expand(workingDir + 'somatic/{sample}/samples.tsv', sample=bamDict.keys()),
        # expand(workingDir + 'calls/{sample}_{type}.bcf', sample=bamDict.keys(), type=TYPES)

rule delly_call:
    input:
        t = get_tumor_bam,
        tIndex = get_tumor_index,
        n = get_normal_bam,
        nIndex = get_normal_index,
        ref = ref
    params:
        tp = '{type}',
        path = execDir + 'sv_callers/'
    output:
        workingDir + 'calls/{sample}_{type}.bcf'
    shell:
        '{params.path}delly_v0.7.7_parallel_linux_x86_64bit call \
            -t {params.tp} \
            -o {output} \
            -g {input.ref} \
            {input.n} {input.t}'
            #-x /DCEG/CGF/Bioinformatics/Production/Bari/Struct_var_pipeline_dev/sv_callers/delly/excludeTemplates/human.hg19.excl.tsv \
            # exclude file?
            # or, wildcards.type instead of params

rule delly_create_tsv:
    input:
        t = get_tumor_bam,
        tIndex = get_tumor_index,
        n = get_normal_bam,
        nIndex = get_normal_index
    output:
        workingDir + 'somatic/{sample}_samples.tsv'
    params:
        path = execDir + 'scripts/'
    shell:
        'module load samtools;'
        '{params.path}create_tsv.sh {input.t} tumor {output};'
        '{params.path}create_tsv.sh {input.n} control {output}'
    # tab-delimited sample description file where the first column is the sample id (as in the VCF/BCF file) and the second column is either tumor or control

rule delly_pre_filter:
    input:
        bcf = workingDir + 'calls/{sample}_{type}.bcf',
        tsv = workingDir + 'somatic/{sample}_samples.tsv'
    output:
        workingDir + 'somatic/{sample}_{type}.bcf'
    params:
        tp = '{type}',
        path = execDir + 'sv_callers/'
    shell:
        '{params.path}delly_v0.7.7_parallel_linux_x86_64bit filter \
            -t {params.tp} \
            -f somatic \
            -o {output} \
            -s {input.tsv} \
            {input.bcf}'
            # tab-delimited sample description file where the first column is the sample id (as in the VCF/BCF file) and the second column is either tumor or control

rule delly_combine_output:
    input:
        expand(workingDir + 'somatic/{{sample}}_{type}.bcf', type=TYPES)
    output:
        workingDir + 'somatic/{sample}_all.txt'
    shell:
        'module load bcftools;'
        'bcftools concat {input} > {output}'

rule list_output_files:
    input:
        workingDir + 'somatic/{sample}_all.txt'
    output:
        temp(parentDir + 'SV_files_for_annotation_delly.{sample}.txt')
    shell:
        'echo {input} | tr " " "\n" | sed "s/^/{wildcards.sample} /" > {output}'

rule merge_list:
    input:
        expand(parentDir + 'SV_files_for_annotation_delly.{sample}.txt', sample=bamDict.keys())
    output:
        parentDir + 'SV_files_for_annotation_delly.txt'
    shell:
        'cat {input} | sed "1s/^/sample delly\\n/" > {output}'


# rule delly_re_genotype:
#     input:
#     output:
#     shell:
#         'delly call -t DEL -g hg19.fa -v t1.pre.bcf -o geno.bcf -x hg19.excl tumor1.bam control1.bam ... controlN.bam'

# rule delly_post_filter:
#     input:
#     output:
#     shell:
#         'delly filter -t DEL -f somatic -o t1.somatic.bcf -s samples.tsv geno.bcf'