#!/usr/bin/env python3

mantaWkngDir = 'manta_germline/'
mantaTempDir = config['tempDir'].rstrip('/') + '/manta_germline/'
i = CALLERS.index("manta")
mantaNt = config['maxThreads'][i]

rule manta_filter_bams:
    input:
        get_bam
    output:
        mantaWkngDir + 'filtered_bams/{sample}.bam'
    params:
        path = execBindPath + 'scripts/',
        bam = get_bam_container
    singularity: 'shub://bballew/NGS_singularity_recipes:samtools_1-9'
    shell:
        '{params.path}filter_bams.sh {params.bam} {output}'

rule manta_index_filtered_bams:
    input:
        mantaWkngDir + 'filtered_bams/{sample}.bam'
    output:
        mantaWkngDir + 'filtered_bams/{sample}.bam.bai'
    singularity: 'shub://bballew/NGS_singularity_recipes:samtools_1-9'
    shell:
        'samtools index {input}'

rule manta_create_run_script:
    input:
        bam = mantaWkngDir + 'filtered_bams/{sample}.bam',
        bai = mantaWkngDir + 'filtered_bams/{sample}.bam.bai',
        ref = ref,
        fai = ref + '.fai'
    output:
        temp(mantaTempDir + '{sample}/runWorkflow.py')
    params:
        outParam = tempBindPath + 'manta_germline/{sample}',
        ref = refBindPath + refFile
    singularity: 'shub://bballew/NGS_singularity_recipes:manta_1-4-0'
    shell:
        'configManta.py \
            --bam {input.bam} \
            --referenceFasta {params.ref} \
            --runDir {params.outParam}'

rule manta_call:
    '''
    '''
    input:
        mantaTempDir + '{sample}/runWorkflow.py'
    output:
        temp(mantaTempDir + '{sample}/runWorkflow.py'),
        temp(mantaTempDir + '{sample}/runWorkflow.py.config.pickle'),
        temp(mantaTempDir + '{sample}/workflow.error.log.txt'),
        temp(mantaTempDir + '{sample}/workflow.exitcode.txt'),
        temp(mantaTempDir + '{sample}/workflow.warning.log.txt'),
        temp(mantaTempDir + '{sample}/workspace/pyflow.data/logs/pyflow_log.txt'),
        temp(mantaTempDir + '{sample}/results/variants/candidateSmallIndels.vcf.gz'),
        temp(mantaTempDir + '{sample}/results/variants/candidateSmallIndels.vcf.gz.tbi'),
        temp(mantaTempDir + '{sample}/results/variants/candidateSV.vcf.gz'),
        temp(mantaTempDir + '{sample}/results/variants/candidateSV.vcf.gz.tbi'),
        temp(mantaTempDir + '{sample}/results/variants/diploidSV.vcf.gz'),
        temp(mantaTempDir + '{sample}/results/variants/diploidSV.vcf.gz.tbi'),
        temp(mantaTempDir + '{sample}/results/stats/alignmentStatsSummary.txt'),
        temp(mantaTempDir + '{sample}/results/stats/svCandidateGenerationStats.tsv'),
        temp(mantaTempDir + '{sample}/results/stats/svCandidateGenerationStats.xml'),
        temp(mantaTempDir + '{sample}/results/stats/svLocusGraphStats.tsv')
    threads: mantaNt
    singularity: 'shub://bballew/NGS_singularity_recipes:manta_1-4-0'
    params:
        tempBindPath + 'manta_germline/{sample}/runWorkflow.py'
    shell:
        '{params} -m local -j {threads}' 

rule manta_move_call_files:
    '''Use cp -l instead of mv
    The way our cluster separates storage pools can create an occasional
    Input/output error with mv.  Use cp -l instead, then delete the temps.

    NEVERMIND.  Cluster config change.  New scratch space is on different
    device; can't hard link.
    '''
    input:
        i1 = mantaTempDir + '{sample}/runWorkflow.py',
        i2 = mantaTempDir + '{sample}/runWorkflow.py.config.pickle',
        i3 = mantaTempDir + '{sample}/workflow.error.log.txt',
        i4 = mantaTempDir + '{sample}/workflow.exitcode.txt',
        i5 = mantaTempDir + '{sample}/workflow.warning.log.txt',
        i6 = mantaTempDir + '{sample}/results/variants/candidateSmallIndels.vcf.gz',
        i7 = mantaTempDir + '{sample}/results/variants/candidateSmallIndels.vcf.gz.tbi',
        i8 = mantaTempDir + '{sample}/results/variants/candidateSV.vcf.gz',
        i9 = mantaTempDir + '{sample}/results/variants/candidateSV.vcf.gz.tbi',
        i10 = mantaTempDir + '{sample}/results/variants/diploidSV.vcf.gz',
        i11 = mantaTempDir + '{sample}/results/variants/diploidSV.vcf.gz.tbi',
        i12 = mantaTempDir + '{sample}/results/stats/alignmentStatsSummary.txt',
        i13 = mantaTempDir + '{sample}/results/stats/svCandidateGenerationStats.tsv',
        i14 = mantaTempDir + '{sample}/results/stats/svCandidateGenerationStats.xml',
        i15 = mantaTempDir + '{sample}/results/stats/svLocusGraphStats.tsv'
    output:
        mantaWkngDir + '{sample}/runWorkflow.py',
        mantaWkngDir + '{sample}/runWorkflow.py.config.pickle',
        mantaWkngDir + '{sample}/workflow.error.log.txt',
        mantaWkngDir + '{sample}/workflow.exitcode.txt',
        mantaWkngDir + '{sample}/workflow.warning.log.txt',
        mantaWkngDir + '{sample}/results/variants/candidateSmallIndels.vcf.gz',
        mantaWkngDir + '{sample}/results/variants/candidateSmallIndels.vcf.gz.tbi',
        mantaWkngDir + '{sample}/results/variants/candidateSV.vcf.gz',
        mantaWkngDir + '{sample}/results/variants/candidateSV.vcf.gz.tbi',
        mantaWkngDir + '{sample}/results/variants/diploidSV.vcf.gz',
        mantaWkngDir + '{sample}/results/variants/diploidSV.vcf.gz.tbi',
        mantaWkngDir + '{sample}/results/stats/alignmentStatsSummary.txt',
        mantaWkngDir + '{sample}/results/stats/svCandidateGenerationStats.tsv',
        mantaWkngDir + '{sample}/results/stats/svCandidateGenerationStats.xml',
        mantaWkngDir + '{sample}/results/stats/svLocusGraphStats.tsv'
    params:
        wkngDir = mantaWkngDir + '{sample}/',
        wkngDir1 = mantaWkngDir + '{sample}/results/',
        tempDir1 = mantaTempDir + '{sample}/results/stats/',
        tempDir2 = mantaTempDir + '{sample}/results/variants/'
    shell:
        'cp -r {params.tempDir1} {params.wkngDir1};'
        'cp -r {params.tempDir2} {params.wkngDir1};'
        'cp {input.i1} {input.i2} {input.i3} {input.i4} {input.i5} -t {params.wkngDir}'

rule manta_list_output_files:
    input:
        mantaWkngDir + '{sample}/results/variants/diploidSV.vcf.gz'
    output:
        temp(parentDir + 'SV_files_for_annotation_manta.{sample}.txt')
    params:
        path = execDir + 'scripts/'
    shell:
        '{params.path}list_files.sh {input} {wildcards.sample} > {output}'

rule manta_merge_list:
    input:
        expand(parentDir + 'SV_files_for_annotation_manta.{sample}.txt', sample=bamDict.keys())
    output:
        parentDir + 'SV_files_for_annotation_manta.txt'
    shell:
        'cat {input} | sort | sed "1s/^/sample manta\\n/" > {output}'